plugins {
    id 'java'
    id 'java-library'
    id 'jacoco'
    id 'me.champeau.jmh' version '0.6.6'
    id 'org.sonarqube' version '3.4.0.2513'
    id 'com.diffplug.spotless' version '6.10.0'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

def readJavaLicense = licenseName -> {
    File licenseFile = new File('docs/licenses/' + licenseName + '.txt')
    def line
    def s = '/*\n'
    licenseFile.withReader { reader ->
        while ((line = reader.readLine()) != null) {
            s += ' *'
            if(!line.isEmpty()) {
                s += ' '
                s += line
            }
            s += '\n'
        }
    }
    s += ' */'
    return s
}

def readPythonLicense = licenseName -> {
    File licenseFile = new File('docs/licenses/' + licenseName + '.txt')
    def line
    def s = '#\n'
    licenseFile.withReader { reader ->
        while ((line = reader.readLine()) != null) {
            s += '#'
            if(!line.isEmpty()) {
                s += ' '
                s += line
            }
            s += '\n'
        }
    }
    s += '#'
    return s
}


java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
    withJavadocJar()
    withSourcesJar()
}

group = 'com.dynatrace.hash4j'
version = '0.6.0'

spotless {
    ratchetFrom 'origin/main'
    python {
        target 'python/**/*.py'
        black('22.8.0')
        licenseHeader readPythonLicense('DYNATRACE_APACHE_2_0_LICENSE'), '(import|from)'
    }
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat('1.15.0')
        licenseHeader readJavaLicense('DYNATRACE_APACHE_2_0_LICENSE')
        targetExclude 'src/main/java/com/dynatrace/hash4j/hashing/Komihash4_3.java','src/main/java/com/dynatrace/hash4j/random/SplitMix64V1.java'
    }
    format 'javaKomihash', com.diffplug.gradle.spotless.JavaExtension, {
        importOrder()
        removeUnusedImports()
        googleJavaFormat('1.15.0')
        licenseHeader readJavaLicense('DYNATRACE_APACHE_2_0_LICENSE') + '\n\n' + readJavaLicense('KOMIHASH_MIT_LICENSE')
        target 'src/main/java/com/dynatrace/hash4j/hashing/Komihash4_3.java'
    }
    format 'javaSplitMix64', com.diffplug.gradle.spotless.JavaExtension, {
        importOrder()
        removeUnusedImports()
        googleJavaFormat('1.15.0')
        licenseHeader readJavaLicense('DYNATRACE_APACHE_2_0_LICENSE') + '\n\n' + readJavaLicense('SPLITMIX64_CREATIVE_COMMONS_LICENSE')
        target 'src/main/java/com/dynatrace/hash4j/random/SplitMix64V1.java'
    }
}

sonarqube {
  properties {
    property 'sonar.projectKey', 'dynatrace-oss_hash4j'
    property 'sonar.organization', 'dynatrace-oss'
    property 'sonar.host.url', 'https://sonarcloud.io'
  }
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.9.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.23.1'
    testImplementation group: 'org.hipparchus', name: 'hipparchus-stat', version: '2.2'
    testImplementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    testImplementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
    testImplementation group: 'net.openhft', name: 'zero-allocation-hashing', version: '0.16'
    testImplementation group: 'com.appmattus.crypto', name: 'cryptohash', version: '0.10.1'
    testImplementation group: 'org.greenrobot', name: 'essentials', version: '3.1.0'
}

jacocoTestReport { // necessary for sonarcloud coverage
    reports {
        xml.required = true
        csv.required = true
    }
}

test {
    useJUnitPlatform()
}

jmh {
    fork = 1
    timeUnit = 'us'
    failOnError = false
    timeOnIteration = '1s'
    warmupForks = 0
    warmupIterations = 5
    warmupBatchSize = 1
    warmup = '1s'
    iterations = 20
}

task evaluateBenchmarks(type:Exec) {
    group "evaluation"
    workingDir '.'
    commandLine 'python3', 'python/benchmark_evaluation.py'
}

task evaluateEstimationErrors(type:Exec) {
    group "evaluation"
    workingDir '.'
    commandLine 'python3', 'python/estimation_error_evaluation.py'
}

task simulateEstimationErrors(type: JavaExec) {
    group "evaluation"
    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'com.dynatrace.hash4j.distinctcount.UltraLogLogEstimationErrorSimulation'
}

tasks.register('checkStatusForBenchmarks') {
    outputs.upToDateWhen { false }
    doLast {
        def status_text = "git status --porcelain".execute().text
        if(status_text?.trim()) {
            throw new GradleException('There are uncommitted changes:\n' + status_text)
        }
    }
}

tasks.register('copyBenchmarkReport', Copy) {
    def proc = "git rev-parse HEAD".execute()
    def revision = proc.text.trim()
    rename ('results.txt', new Date().format('yyyy-MM-dd-HH-mm-ss') + ' ' + revision + '.txt')
    from file('build/results/jmh/results.txt')
    into file('benchmark-results/')
}

tasks.register('deleteBenchmarkReport', Delete) {
    delete 'build/results/jmh/results.txt'
}

tasks.test.finalizedBy jacocoTestReport
tasks.jmh.finalizedBy copyBenchmarkReport
tasks.copyBenchmarkReport.finalizedBy deleteBenchmarkReport
tasks.deleteBenchmarkReport.finalizedBy evaluateBenchmarks
check.dependsOn jacocoTestCoverageVerification
tasks.jmh.dependsOn checkStatusForBenchmarks
tasks.copyBenchmarkReport.dependsOn checkStatusForBenchmarks
tasks.simulateEstimationErrors.finalizedBy evaluateEstimationErrors

sourceSets {
    jmh
    main
    test
}

javadoc {
    failOnError true
    title 'hash4j ' + project.version + ' API'
}

jacoco {
    toolVersion = '0.8.8'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 1.0
                counter = 'LINE'
            }
            limit {
                minimum = 1.0
                counter = 'BRANCH'
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'com.dynatrace.hash4j:hash4j'
                description = 'hash4j: A Dynatrace hash library for Java'
                url = 'https://github.com/dynatrace-oss/hash4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'Dynatrace'
                        name = 'Dynatrace LLC'
                        email = 'opensource@dynatrace.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/dynatrace-oss/hash4j.git'
                    developerConnection = 'scm:git:ssh://github.com/dynatrace-oss/hash4j.git'
                    url = 'https://github.com/dynatrace-oss/hash4j'
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(System.getenv('GPG_PRIVATE_KEY'), System.getenv('GPG_PASSPHRASE'))
    sign publishing.publications.mavenJava
}

nexusPublishing {
    packageGroup = 'com.dynatrace'
    useStaging = true
    repositories {
        sonatype {
            nexusUrl = uri('https://oss.sonatype.org/service/local/')
            snapshotRepositoryUrl = uri('https://oss.sonatype.org/content/repositories/snapshots/')
            username = System.getenv('OSSRH_USERNAME')
            password = System.getenv('OSSRH_PASSWORD')
        }
    }
}
